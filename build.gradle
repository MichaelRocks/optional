buildscript {
  ext.kotlinVersion = '1.0.3'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

plugins {
  id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

group 'io.michaelrocks'
version '1.0.1'
sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  testCompile 'io.kotlintest:kotlintest:1.3.3'
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
  bintray {
    user = project.property('bintrayUser')
    key = project.property('bintrayKey')
    publications = ['mavenJava']

    pkg {
      repo = 'maven'
      name = project.name

      version {
        name = project.version
        released = new Date()
        vcsTag = "v${project.version}"
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId project.name
      from components.java

      artifact sourcesJar {
        classifier = 'sources'
      }
      artifact javadocJar {
        classifier = 'javadoc'
      }
    }
  }
}
